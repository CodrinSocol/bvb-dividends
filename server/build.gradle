import org.codehaus.groovy.runtime.GStringImpl

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.bvb'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(22)
	}
}

ext.jaxwsSourceDir = "${layout.buildDirectory.get()}/generated/sources/jaxws" as GStringImpl

configurations {
	jaxws
}

repositories {
	mavenCentral()
}

tasks.register('wsimport') {
	description = 'Generate classes from wsdl using wsimport'

	doLast {
		project.mkdir(jaxwsSourceDir)
		ant {
			taskdef(name: 'wsimport',
					classname: 'com.sun.tools.ws.ant.WsImport',
					classpath: configurations.jaxws.asPath
			)
			wsimport(
					keep: true,
					destdir: jaxwsSourceDir,
					extension: "true",
					verbose: true,
					wsdl: "https://ws.bvb.ro/BVBFinancialsWS/Financials.asmx?WSDL",
					xnocompile: true,
					package: "com.bvb.bvbdividends.wsdl") {
				xjcarg(value: "-XautoNameResolution")
			}
		}
	}
}

sourceSets {
	main {
		java.srcDirs += jaxwsSourceDir
	}
}

compileJava {
	dependsOn wsimport
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
	implementation 'org.modelmapper:modelmapper:2.4.2'
	implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'

	developmentOnly("org.springframework.boot:spring-boot-devtools")

	jaxws 'com.sun.xml.ws:jaxws-tools:4.0.2',
			'jakarta.xml.ws:jakarta.xml.ws-api:4.0.2',
			'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2',
			'jakarta.activation:jakarta.activation-api:2.1.3',
			'com.sun.xml.ws:jaxws-rt:4.0.2'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
